plugins {
    id 'java-library'
}
sourceCompatibility = JavaVersion.VERSION_1_8

group 'io.github.yeamy'
version '1.0'

repositories {
    mavenCentral()
}

dependencies {
    api 'org.apache.httpcomponents.client5:httpclient5:5.2.1'
//    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.0'
//    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.0'
}

//test {
//    useJUnitPlatform()
//}

apply plugin: 'maven-publish'

jar {// package code
    archiveFileName = 'lib.jar'
}

task generateSourceJar(type: Jar) {
    archiveClassifier.set('sources')
    from sourceSets.main.java.srcDirs
    archiveFileName = 'lib-sources.jar'
}
javadoc {// create document
    source = sourceSets.main.java.srcDirs
    options {
        encoding "UTF-8"
        charSet 'UTF-8'
        author true
        version true
        links "https://docs.oracle.com/javase/8/docs/api/index.html"
    }
}
task generateJavadoc(type: Jar) { // package document as jar
    group = 'jar'
    archiveFileName = 'lib-javadoc.jar'
    archiveClassifier.set('javadoc')
    from "$buildDir/docs/javadoc"
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId this.group
            artifactId 'httpclient-apt'
            version this.version
            pom {
                name = 'httpclient-apt'
                description = 'Generate a simple http client with Java APT.'
                url = 'https://github.com/Yeamy/httpclient-apt'
                inceptionYear = '2022'
                scm {
                    url = 'https://github.com/Yeamy/httpclient-apt'
                    connection = 'scm:git@https://github.com/Yeamy/httpclient-apt.git'
                    developerConnection = 'scm:git@https://github.com/Yeamy/httpclient-apt.git'
                }
                licenses {
                    license {
                        name = 'MIT'
                        url = 'https://mit-license.org/'
                        distribution = 'repo'
                        comments = 'A business-friendly OSS license'
                    }
                }
                developers {
                    developer {
                        id = 'yeamy0754'
                        name = 'Yeaming'
                    }
                }
                pom.withXml {
                    def ds = asNode().appendNode("dependencies")
                    configurations.implementation.allDependencies.forEach(){
                        Dependency d -> if(d.version!='unspecified'){
                            def dx = ds.appendNode("dependency")
                            dx.appendNode("groupId", d.group)
                            dx.appendNode("artifactId", d.name)
                            dx.appendNode("version", d.version)
                        }
                    }
                }
            }
            artifact generateSourceJar
            artifact jar
            artifact generateJavadoc
        }
    }

    repositories {
        maven {
            url = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
            credentials {
                username ossrhUsername
                password ossrhPassword
            }
        }
    }
}

apply plugin: 'signing'
signing {
    sign publishing.publications.maven
}

